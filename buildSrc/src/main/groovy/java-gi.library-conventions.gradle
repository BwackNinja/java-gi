import org.apache.tools.ant.taskdefs.condition.Os
import io.github.jwharm.javagi.generator.GenerateSources

/* Common build settings for Java-GI modules:
 * 
 * - Load java-library and maven-publish plugins
 * - Set maven repositories
 * - Load common dependencies
 * - Set group and Java-GI version number
 * - Set JDK version
 * - Configure 'generateSources' action
 * - Enable preview features
 * - Set OS-specific library paths for unit tests
 * - Set common POM metadata
 */

plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.+'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

group = 'io.github.jwharm.javagi'
version = '0.7-SNAPSHOT'

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

tasks.register('generateSources', GenerateSources) {
    // Input directory should point to a clone of the GitHub gircore/gir-files
    // repository. The location is set in gradle.properties. Default is '../gir-files'
    inputDirectory = project.rootDir.toPath().resolve(girFilesLocation).toFile()

    // Write generated sources to build/generated/sources/java-gi
    outputDirectory = layout.buildDirectory.get().dir('generated').dir('sources').dir('java-gi').asFile
}

// Add the generated sources to the main sourceSet
sourceSets.main.java.srcDirs += ['build/generated/sources/java-gi']

tasks.named('compileJava') {
    dependsOn generateSources
    options.compilerArgs += '--enable-preview'
    options.encoding = 'UTF-8'
}

tasks.named('sourcesJar') {
    dependsOn generateSources
}

tasks.named('compileTestJava') {
    options.compilerArgs += '--enable-preview'
    options.encoding = 'UTF-8'
}

tasks.named('test') {
    // Don't run tests in Github action. The native libraries aren't installed.
    if (System.getenv('CI')) {
        enabled = false
    }

    // Configure library path for MacOS (Homebrew) and set MacOS-specific JVM parameter
    if (Os.isFamily(Os.FAMILY_MAC)) {
        jvmArgs += '-Djava.library.path=/opt/homebrew/lib'
        jvmArgs += '-XstartOnFirstThread'
    }

    // Configure library path for Arch, Fedora and Debian/Ubuntu
    else if (Os.isFamily(Os.FAMILY_UNIX)) {
        jvmArgs += '-Djava.library.path=/usr/lib64:/lib64:/lib:/usr/lib:/lib/x86_64-linux-gnu'
    }

    // Configure library path for Windows (MSYS2)
    else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        jvmArgs += '-Djava.library.path=C:/msys64/mingw64/bin'
    }

    useJUnitPlatform()
    jvmArgs += '--enable-preview'
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}

tasks.named('javadoc') {
    // Do not generate javadoc for individual modules.
    // Javadoc is generated once for all modules together (see top-level build file).
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                groupId = 'io.github.jwharm.javagi'
                description = 'Java language bindings for GObject-Introspection-based libraries'
                url = 'https://jwharm.github.io/java-gi/'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 2.1'
                        url = 'https://www.gnu.org/licenses/lgpl-2.1.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jwharm'
                        name = 'Jan-Willem Harmannij'
                        url = 'https://github.com/jwharm'
                    }
                }
            }
        }
    }
}
