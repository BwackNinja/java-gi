import io.github.jwharm.javagi.operations.GenerateSourcesOperation;
import io.github.jwharm.javagi.patches.*;

plugins {
    id 'java-gi.library-conventions'
}

dependencies {
    api project(':glib')
}

version = "1.20-${version}"

tasks.named('generateSources') {
    def MODULE_INFO = """
        module org.freedesktop.gstreamer {
            requires static org.jetbrains.annotations;
            requires transitive org.gnome.glib;
            %s
        }
        """
    
    new GenerateSourcesOperation()
        .sourceDirectory(new File(girFilesLocation).toPath())
        .outputDirectory(buildDir.toPath().resolve('generated').resolve('sources').resolve('java-gi'))

        .source('GLib-2.0.gir', 'https://docs.gtk.org/glib/', false, new GLibPatch())
        .source('GObject-2.0.gir', 'https://docs.gtk.org/gobject/', false, new GObjectPatch())
        .source('Gio-2.0.gir', 'https://docs.gtk.org/gio/', false, new GioPatch())
        .source('GModule-2.0.gir', null, false, null)

        .source('Gst-1.0.gir', null, true, new GstPatch())
        .source('GstBase-1.0.gir', null, true, null)
        .source('GstAudio-1.0.gir', null, true, new GstAudioPatch())
        .source('GstPbutils-1.0.gir', null, true, null)
        .source('GstVideo-1.0.gir', null, true, null)

        .moduleInfo(MODULE_INFO)
        .execute()
}

tasks.named('javadoc') {
    options.linksOffline('https://jwharm.github.io/java-gi/glib', project(':glib').javadoc.outputDirectory.getAbsolutePath())
    dependsOn ':glib:javadoc'
}

tasks.named('compileJava') {
    dependsOn generateSources
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = name
            version = version
        }
    }
}
