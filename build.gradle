/* The top-level project is used to generate javadoc.
 */

plugins {
    id 'java-library'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

// Disable jar for top-level project
tasks.named('jar') {
    enabled = false
}

// Generate javadoc for all modules
tasks.named('javadoc', Javadoc) {
    options.addStringOption('source', '20')
    options.addBooleanOption('-enable-preview', true)
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'

    // Prevent "too many module declarations found" javadoc error
    exclude "**/module-info.java"
    options.addStringOption('-add-modules', 'org.jetbrains.annotations,org.freedesktop.cairo')

    source subprojects.collect {
        it.sourceSets.main.allJava
    }

    classpath = files(subprojects.collect {
        it.sourceSets.main.compileClasspath.filter(file -> {
            // Some modules depend on the cairo java bindings, which in turn has an optional
            // dependency on glib. Because glib is also in the classpath as a module in the
            // current build, it is duplicate. So we remove the jar from the classpath.
            String name = file.getName()
            return (! (name.startsWith("glib-") && name.endsWith(".jar")))
        })
    })

    subprojects.forEach {
        dependsOn it.tasks.named('generateSources')
    }
}
