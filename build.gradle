/* The top-level project is used to generate javadoc.
 */

plugins {
    id 'java-library'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Disable jar for top-level project
tasks.named('jar') {
    enabled = false
}

// Generate javadoc for all modules
tasks.named('javadoc', Javadoc) {
    options.addStringOption('source', '21')
    options.addBooleanOption('-enable-preview', true)
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'

    // Prevent "too many module declarations found" javadoc error
    exclude "**/module-info.java"
    options.addStringOption('-add-modules', 'org.jetbrains.annotations,org.freedesktop.cairo')

    source subprojects.collect {
        it.sourceSets.main.allJava
    }

    classpath = files(subprojects.collect {
        it.sourceSets.main.compileClasspath.filter(file -> {
            def path = file.getAbsolutePath()
            return path.contains('/org.jetbrains/annotations/') || path.contains('/io.github.jwharm.cairobindings/cairo/')
        })
    })

    subprojects.forEach {
        dependsOn it.tasks.named('generateSources')
    }
}
