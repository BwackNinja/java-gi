import io.github.jwharm.javagi.operations.GenerateSourcesOperation;
import io.github.jwharm.javagi.patches.*;

plugins {
    id 'java-gi.library-conventions'
}

dependencies {
    implementation project(':glib')
    implementation 'com.github.jwharm:cairo-java-bindings:1.16.0'
}

version = "4.10-${version}"

tasks.register('generateSources') {
    def MODULE_INFO = """
        module org.gnome.gtk {
            requires static org.jetbrains.annotations;
            requires transitive org.gnome.glib;
            requires transitive org.freedesktop.cairo;
            exports io.github.jwharm.javagi.gtk.annotations;
            exports io.github.jwharm.javagi.gtk.types;
            exports io.github.jwharm.javagi.gtk.util;
            %s
        }
        """
    
    new GenerateSourcesOperation()
        .sourceDirectory(new File(girFilesLocation).toPath())
        .outputDirectory(buildDir.toPath().resolve('generated').resolve('sources').resolve('java-gi'))
        
        .source('GLib-2.0.gir', 'org.gnome.glib', 'https://docs.gtk.org/glib/', false, new GLibPatch())
        .source('GObject-2.0.gir', 'org.gnome.gobject', 'https://docs.gtk.org/gobject/', false, new GObjectPatch())
        .source('Gio-2.0.gir', 'org.gnome.gio', 'https://docs.gtk.org/gio/', false, new GioPatch())
        .source('GModule-2.0.gir', 'org.gnome.gmodule', null, false, null)

        // Do not generate cairo and freetype gir files: These classes are included from cairo-java-bindings
        .source('cairo-1.0.gir', 'org.freedesktop.cairo', null, false, new CairoPatch())
        .source('freetype2-2.0.gir', 'org.freedesktop.freetype', null, false, null)

        .source('HarfBuzz-0.0.gir', 'org.freedesktop.harfbuzz', null, true, new HarfBuzzPatch())
        .source('Pango-1.0.gir', 'org.gnome.pango', 'https://docs.gtk.org/Pango/', true, new PangoPatch())
        .source('PangoCairo-1.0.gir', 'org.gnome.pango.cairo', 'https://docs.gtk.org/Pango/', true, null)
        .source('GdkPixbuf-2.0.gir', 'org.gnome.gdkpixbuf', 'https://docs.gtk.org/gdk-pixbuf/', true, null)
        .source('Gdk-4.0.gir', 'org.gnome.gdk', 'https://docs.gtk.org/gdk4/', true, null)
        .source('Graphene-1.0.gir', 'org.gnome.graphene', 'https://developer-old.gnome.org/graphene/stable/', true, null)
        .source('Gsk-4.0.gir', 'org.gnome.gsk', null, true, null)
        .source('Gtk-4.0.gir', 'org.gnome.gtk', 'https://docs.gtk.org/gtk4/', true, new GtkPatch())

        .moduleInfo(MODULE_INFO)
        .execute()
}

tasks.named('javadoc') {
    options.linksOffline('https://jwharm.github.io/java-gi/glib', project(':glib').javadoc.outputDirectory.getAbsolutePath())
    dependsOn ':glib:javadoc'
}

tasks.named('compileJava') {
    dependsOn generateSources
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = name
            version = version
        }
    }
}
